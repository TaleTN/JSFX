desc:Sample accurate MIDI queue
// (c) Theo Niessink 2012, 2013
// License: GPL - http://www.gnu.org/licenses/gpl.html

/* Example

   desc:Mono synth
   in_pin:none

   import midi_queue.jsfx-inc

   @init
   
   // Set MIDI queue local memory index and size
   q.midiq_init(0, 256);

   @block
   
   // Receive all MIDI messages and add them to the queue
   q.midiq_collect();

   @sample

   while(
     // Remove one MIDI message from the head of the queue
     q.midiq_remove() ? (

       // Parse MIDI message
       q.msg1 &= 0xF0;
       velocity = q.msg23 >> 8;

       // Note On
       q.msg1 == 0x90 && velocity ? (
         note = q.msg23 & 0x7F;
         dt = 440 * 2^((note - 69) / 12) / srate;
       ) :

       // Note Off
       q.msg1 == 0x80 || (q.msg1 == 0x90 && !velocity) ? (
         q.msg23 & 0x7F == note ? dt = 0;
       );

       1; // while q.midiq_remove()
     );
   );

   // Sine wave oscillator
   dt > 0 ? spl0 = spl1 = 0.5 * sin(2*$pi * t);
   t += dt;
   t -= t|0;

   Queue Functions

    * midiq_init(index, size)
      Example: q.midiq_init(0, 256);
      Sets the offset and size of the local memory buffer to store the queue
      in.

      Note: You should call this function from the @init section.

    * midiq_collect()
      Example: q.midiq_collect();
      Receives any MIDI messages, and adds them to the queue.

      Note: You should call this function from the @block section.

    * midiq_remove()
      Example: avail = q.midiq_remove();
      Checks if a MIDI message is available, removes it from the queue, and
      stores it in the msg1 and msg23 instance variables.

      Note: You should call this function in a while loop from the @sample
      section.

   Miscellaneous Functions

    * midiq_rewind()
      Example: q.midiq_rewind();
      Rewinds the queue, so new MIDI messages can be added.

      Note: This function is automatically called by midiq_collect().

    * midiq_add(ofs, msg1, msg23)
      Example: q.midiq_add(ofs, msg1, msg23);
      Adds a MIDI message to the queue.

   Instance Variables

    * msg1
    * msg23
      Example: ch = q.msg1 & 0x0F;
      The MIDI message that was last removed from the queue.

    * ofs
      Example: t = q.ofs / srate;
      The offset within the current sample block, in samples.

*/

@init


function midiq_init(mem, size)
(
  this.buf = mem;

  // Maximum number of MIDI events (0 for unlimited size)
  this.size = size * 3;
);


function midiq_rewind()
  instance(buf, head, tail, ofs)
(
  head = tail = buf;
  ofs = 0;
);


function midiq_add(ofs, msg1, msg23)
  instance(buf, size, tail)
(
  !size || tail < buf + size ? (
    tail[0] = ofs;
    tail[1] = msg1;
    tail[2] = msg23;
    tail += 3;
  );
);


function midiq_collect()
  local(ofs, msg1, msg23)
(
  this.midiq_rewind();

  while(
    midirecv(ofs, msg1, msg23) ? (
      this.midiq_add(ofs, msg1, msg23);
      midisend(ofs, msg1, msg23);
    );
  );
);


function midiq_remove()
  instance(head, tail, ofs, msg1, msg23)
(
  head < tail && head[] <= ofs ? (
    msg1  = head[1];
    msg23 = head[2];
    head += 3;
  ) : (
    ofs += 1;
    0;
  );
);
