desc:Sample accurate MIDI queue
// (c) Theo Niessink 2012-2014
// License: GPL - http://www.gnu.org/licenses/gpl.html

/* Example

   desc:Mono synth

   import midi_queue.jsfx-inc
   import poly_blep.jsfx-inc

   @init

   // Set MIDI queue local memory index and size.
   midiq.midiq_init(0, 256);

   @block

   // Receive MIDI messages and add them to queue.
   midiq.midiq_collect();

   @sample

   // Remove MIDI message from the head of queue.
   while(midiq.midiq_remove()) (

     // Parse MIDI message.
     status = midiq.msg1 & 0xF0;
     velocity = midiq.msg3;

     // Note On
     status == 0x90 && velocity ? (
       note = midiq.msg2;
       osc.poly_setf(440 * 2^((note - 69) / 12));
     ) :

     // Note Off
     status == 0x80 || (status == 0x90 && !velocity) ? (
       midiq.msg2 == note ? osc.poly_setf(0);
     );
   );

   // Square wave oscillator.
   osc.dt > 0 ? spl0 = spl1 = 0.25 * osc.poly_sqr();

   Queue Functions

    * midiq_init(index[, num])
      Example: q.midiq_init(0, 256);
      Sets the offset and size of the local memory buffer to store the queue
      in. Returns the maximum size of the buffer (i.e. num*3), or 0 for
      unlimited size.

      Note: You should call this function from the @init section.

    * midiq_collect()
    * midiq_collect(ch, mask)
      Example: q.midiq_collect();
      Receives any MIDI messages, optionally filtering by channel (0..15,
      <0=any) and status byte (1=Note Off, 2=Note On, 4=Poly Aftertouch,
      8=Control Change, 16=Program Change, 32=Channel Atertouch, 64=Pitch
      Bend).

      Note: You should call this function from the @block section.

    * midiq_remove()
      Example: avail = q.midiq_remove();
      Checks if a MIDI message is available, removes it from the queue, and
      stores it in the msg1, msg2, and msg3 instance variables.

      Note: You should call this function in a while loop from the @sample
      section.

   Miscellaneous Functions

    * midiq_rewind()
      Example: q.midiq_rewind();
      Rewinds the queue, so new MIDI messages can be added.

      Note: This function is automatically called by midiq_collect().

    * midiq_add(ofs, msg1, msg23)
      Example: q.midiq_add(ofs, msg1, msg23);
      Adds a MIDI message to the queue.

   Instance Variables

    * msg1
    * msg2
    * msg3
      Example: ch = q.msg1 & 0x0F;
      The MIDI message that was last removed from the queue.

    * ofs
      Example: t = q.ofs / srate;
      The offset within the current sample block, in samples.

*/

@init


function midiq_init(index, num)
  instance(buf, size)
(
  buf = index;
  size = num * 3;
);

function midiq_init(index)
(
  this.midiq_init(index, 0);
);


function midiq_rewind()
  instance(buf, head, tail, ofs)
(
  head = tail = buf;
  ofs = 0;
);


function midiq_add(ofs, msg1, msg23)
  instance(buf, size, tail)
(
  !size || tail < buf + size ? (
    tail[0] = ofs;
    tail[1] = msg1;
    tail[2] = msg23;
    tail += 3;
  );
);


function midiq_collect(ch, mask)
  local(ofs, msg1, msg23)
(
  this.midiq_rewind();

  while(
    midirecv(ofs, msg1, msg23) ? (
      (ch < 0 || msg1 & 0x0F == ch) && mask & (1 << ((msg1 >> 4) - 8)) ? this.midiq_add(ofs, msg1, msg23);
      midisend(ofs, msg1, msg23);
    );
  );
);

function midiq_collect()
(
  this.midiq_collect(-1, 127);
);


function midiq_remove()
  instance(head, tail, ofs, msg1, msg2, msg3, msg23)
(
  head < tail && head[] <= ofs ? (
    msg1  = head[1];
    msg23 = head[2];
    msg2 = msg23 & 0x7F;
    msg3 = msg23 >> 8;
    head += 3;
  ) : (
    ofs += 1;
    0;
  );
);
